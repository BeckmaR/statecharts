package org.yakindu.base.generator.java.templates

import org.yakindu.base.generator.generator.CodeElement
import org.yakindu.base.generator.generator.SourceFileGen

class SourceFileTemplate extends Template {
	def dispatch generate(SourceFileGen it) { '''
		«generateDocumentation»
		«generatePackage»
		«generateImports»
		«generateContent»
	'''
	}
	
	def generatePackage(SourceFileGen it) {
		if(!package.nullOrEmpty) {
			'''package «package»
			
			'''
		} else ""
	}
	
	def generateImports(SourceFileGen it) {
		if(!imports.nullOrEmpty) {
			'''
			«FOR i:imports»
			import «i»;
			«ENDFOR»
			
			'''
		}
	}
	
	override generateDocumentation(CodeElement it) {
		if(it.documentation.nullOrEmpty) {
			'''
			/*
			 * File «name»
			 * Generated by the YAKINDU Code Generator
			 */
			
			'''
		} else {
			super.generateDocumentation(it)
		}
	}
	
}